/*
 * Factorization program
 * Adapted version of example from "Janus: a time-reversible language" by
 * C. Lutz and H. Derby.
 */

procedure zeroi(int i, int fact[])
    from fact[i+1] = 0 loop
        i -= 1
    until i = 0

procedure nexttryFactor(int tryFactor)
    tryFactor += 2
    if tryFactor = 4 then
        tryFactor -= 1
    fi tryFactor = 3



// factor num into table in fact[]
procedure factor(int num, int fact[])
    local int tryFactor = 0   // Attempted factor.
    local int i = 0     // Pointer to last factor in factor table.
    from (tryFactor = 0) && (num > 1) loop
        call nexttryFactor(tryFactor)
        from fact[i] != tryFactor loop               // Divide out all occurrences of this
            i += 1                             // factor
            fact[i] += tryFactor
            local int z = num / tryFactor
            z <=> num
            delocal int z = num * tryFactor
        until (num % tryFactor) != 0
    until (tryFactor * tryFactor) > num                    // Exit early if possible

    if num != 1 then
        i += 1                                 // Put last prime away, if not done
        fact[i] ^= num                         // and zero num
        num     ^= fact[i]
        fact[i] ^= num
    else
        num -= 1
    fi fact[i] != fact[i-1]

    if (fact[i-1] * fact[i-1]) < fact[i] then  // Zero tryFactor
        from (tryFactor * tryFactor) > fact[i] loop
            uncall nexttryFactor(tryFactor)
        until tryFactor = 0
    else
        tryFactor -= fact[i-1]
    fi (fact[i-1] * fact[i-1]) < fact[i]

    call zeroi(i, fact)                        // Zero i
    delocal int i = 0
    delocal int tryFactor = 0

procedure main()
    int num      // Number to factor.  Ends up zero
    int fact[20] // Factor table.  Starts zero. Ends with factors in ascending order

    num += 840
    call factor(num, fact)

//    printf("%d %d %d %d %d\n", fact[0], fact[1], fact[2], fact[3], fact[4])

    /*
    fact[1] += 2
    fact[2] += 2
    fact[3] += 2
    fact[4] += 3
    fact[5] += 5
    fact[6] += 7
    uncall factor(num, fact)
    */
